*GB Template
*# Pray-File "GB_BalloonBug.agent"
*# DS-Name "Balloon Bug"
*# Depend balloonbug.c16
*# Attach gb_c3_conv.c16

*# desc = "C3 Balloon Bug raw, buggy, unsupported conversion."
*# Agent Animation File = "gb_c3_conv.c16"
*# Agent Animation Gallery = "gb_c3_conv"
*# Agent Sprite First Image = 1
*# GB_Category = 3
*# Agent Author = "Creature Labs"

new: simp 2 13 9 "balloonbug" 42 0 3000
attr 198
bhvr 59
clac 0

elas 0

setv ov61 40
mvsf game "ch_x" game "ch_y"

tick 5

setv ov10 rand 0 1
doif ov10 eq 0
	setv ov10 -1
endi
setv ov11 rand 0 1
doif ov11 eq 0
	setv ov11 -1
endi



**100% copypasted scripts
scrp 2 13 9 1
	doif ov00 eq 0

		setv ov91 100
	endi
	targ from
	doif fmly eq 4
		stim writ targ 85 1
	endi
endm



scrp 2 13 9 9

	addv ov01 1

	doif ov01 ge 100
		doif totl 2 13 9 lt 20
			rnge 1000
			setv va99 0
			esee 2 13 9
				addv va99 1
			next

			doif va99 le 10
				setv va98 totl 2 18 24

				doif va98 lt 3

					inst
					setv va90 posl
					setv va91 post
					new: simp 2 18 24 "blnk" 1 0 0
					doif tmvt va90 va91 <> 1
						kill targ
						stop
					endi
					attr 16
					tick 3600
					mvto va90 va91
				endi
			endi
		endi
	endi
	targ ownr
	
*die if older than 5 to 15 minutes old
	doif ov01 ge rand 1200 3600
		gsub die
		stop
	endi

	subv ov02 1


	doif ov02 le rand 100 200 and ov00 ne 3 and ov00 ne 1

		doif ov00 eq 1
			setv ov91 100
		endi

		setv va00 10000
		seta va02 null
		inst
		esee 2 7 0
			setv va01 rely ownr targ
			doif va01 lt 0
				negv va01
			endi
			doif va01 lt 100
				setv va01 relx ownr targ
				doif va01 lt 0
					negv va01
				endi
				doif va01 lt va00
					setv va00 va01
					seta va02 targ
				endi
			endi
		next
		slow
		doif va02 ne null
			seta ov16 va02
			setv ov00 3
		else
			setv ov00 0
			setv ov91 0
		endi
	endi


	doif ov00 eq 3
		setv ov14 3
		setv ov15 3
		gsub home
		gsub velo

		inst
		doif ov16 ne null
			doif touc ov16 ownr gt 0
				mesg writ ov16 303
				setv ov00 0
				addv ov02 100
			endi
		else
			setv ov00 0
		endi
		slow
	endi




	doif ov00 eq 1
		addv ov91 1

		doif ov91 ge rand 30 50
			doif ov10 lt 0
				anim [8 9 10 11 12 13 14 15 16]
				accg 0.3
			else
				anim [22 23 24 25 26 27 28 29 30]
				accg 0.3
			endi
			setv vely -10
			over
			setv ov00 0
			setv ov91 0
		endi

		doif fall eq 0
			doif ov10 lt 0
				gsub left
			else
				gsub rite
			endi
		endi

	endi



	doif ov00 eq 0
		addv ov91 1

		doif ov91 ge rand 30 50
			setv ov91 0
			setv ov00 1
			accg 3
			doif ov12 lt 0
				pose 0
			else
				pose 4
			endi
			stop


		endi

		setv ov14 5
		setv ov15 5

		subv ov90 1

		gsub wall

		doif ov90 le 0

			doif va00 ne 1
				doif rand 0 2 eq 0

					negv ov10
				endi
				addv ov90 rand 1 3
			endi

			doif va01 ne 1


				negv ov11

				addv ov90 rand 2 5
			endi
		endi

		gsub velo
		gsub anim


	endi



	subr left
		anim [0 1 2 3]
		velo -3 -5
	retn


	subr rite
		anim [4 5 6 7]
		velo 3 -5
	retn



	subr velo
		doif ov10 lt 0

			negv ov14
			doif ov12 gt ov14
				subv ov12 2
			endi
			negv ov14
		else

			doif ov12 lt ov14
				addv ov12 2
			endi
		endi

		doif ov11 lt 0

			negv ov15
			doif ov13 gt ov15
				subv ov13 2
			endi
			negv ov15
		else

			doif ov13 lt ov15
				addv ov13 2
			endi
		endi


		setv velx ov12
		setv vely ov13
	retn


	subr wall

		doif obst 0 lt 100 and ov10 lt 0
			negv ov10
			setv va00 1
		endi

		doif obst 1 lt 100 and ov10 gt 0
			negv ov10
			setv va00 1
		endi

		doif obst 2 lt 50 and ov11 lt 0
			negv ov11
			setv va01 1
		endi

		doif obst 3 lt 100 and ov11 gt 0
			negv ov11
			setv va01 1
		endi
	retn


	subr anim

		doif ov12 gt 0
			anim [31 32 33 34 35 255]
		elif ov12 lt 0
			anim [17 18 19 20 21 255]
		endi
	retn


	subr home
		inst
		targ ov16
		doif targ ne null
			setv va00 relx ownr targ


			targ ownr
			doif va00 lt 0
				doif ov10 gt 0
					negv ov10
				endi
			else
				doif ov10 lt 0
					negv ov10
				endi
			endi

			doif ov16 eq null
				setv ov00 0
				stop
			endi
			setv va00 rely ownr ov16

			targ ownr
			doif va00 lt 0
				doif ov11 gt 0
					negv ov11
				endi
			else
				doif ov11 lt 0
					negv ov11
				endi
			endi

		else
			targ ownr
			setv ov00 0
		endi
		slow
	retn
endm


scrp 2 18 24 9
	setv va00 posl
	setv va01 post
	inst
	new: simp 2 13 9 "balloonbug" 42 0 3000
	attr 198
	clac 0

	elas 0

	setv ov61 40
	mvto va00 va01



	setv ov10 rand 0 1
	doif ov10 eq 0
		setv ov10 -1
	endi
	setv ov11 rand 0 1
	doif ov11 eq 0
		setv ov11 -1
	endi

	tick 5
	kill ownr
endm




rscr
enum 2 13 9
	kill targ
next
*scrx 2 13 9 9
