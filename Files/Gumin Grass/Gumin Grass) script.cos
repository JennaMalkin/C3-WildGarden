*GB Template
*# Pray-File "GB_Gumingrass.agent"
*# DS-Name "Gumin Grass)"
*# Depend gumin.c16
*# Attach gb_c3_conv2.c16

*# desc = "C3 Gumin Grass raw, buggy, unsupported conversion."
*# Agent Animation File = "gb_c3_conv2.c16"
*# Agent Animation Gallery = "gb_c3_conv2"
*# Agent Sprite First Image = 7
*# GB_Category = 4
*# Agent Author = "Creature Labs"

new: simp 2 4 10 "gumin" 95 0 4200
attr 197
perm 100
elas 20
clac -1
accg 1
aero 5
fric 100

setv ov01 150
setv ov61 50
setv ov90 3
tick 1
mvsf game "ch_x" game "ch_y"

*Plant timer script
scrp 2 4 10 9
*Lose life energy
	subv ov01 1
*Check energy stages for growth and trigger animations/reproduction
	doif ov01 <= 230 and ov01 >= 221

		setv ov90 1
		setv ov30 0
	elif ov01 <= 220 and ov01 >= 201 and ov90 = 1
		setv ov90 2
		setv ov30 19
		wait rand 30 60
	elif ov01 <= 200 and ov01 >= 171 and ov90 = 2
		setv ov90 3
		setv ov30 38
		wait rand 30 60
	elif ov01 <= 170 and ov01 >= 121 and ov90 = 3
		setv ov90 4
		setv ov30 57
		wait rand 30 60
	elif ov01 <= 110 and ov01 >= 50 and ov90 = 4
		setv ov30 76
*If old enough, animate and if not already reproduced, run reproduction subroutine
		gsub anim
		doif ov91 <> 1
			wait rand 10 60
			gsub seed
		endi
*If out of energy, die.
	elif ov01 <= -50
		base 0
		frat 10
		anim [76 76 57 57 38 38 19 19 0 0]
		over
		kill ownr
	endi
	gsub anim
*animation subroutine - sway in the water.
	subr anim
		wait rand 1 5
		frat 3
		base ov30
		anim [0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18]
		over
	retn


*seed subroutine
	subr seed
*Mark as already having reproduced
		setv ov91 1
*temporary location
		setv va40 posl
		setv va41 posl
		setv va42 post
		setv va43 post
		addv va40 4
		addv va40 9
		subv va42 20
		subv va43 50
*Count how many seeds and plants there are nearby
		setv va50 0
		rnge 500
		esee 2 3 8
			addv va50 1
		next
		rnge 500
		esee 2 4 10
			addv va50 1
		next
*If seeds and plants nearby is less than or equal to 5, allow for two new seeds.
		doif va50 <= 5
			setv va51 2
*If plants are between 6 to 11, randomly choose between 1 and 2 seeds.
		elif va50 >= 6 and va50 <= 8
			setv va51 rand 1 2
		elif va50 >= 9 or va50 <= 11
			setv va51 rand 1 2
*Overpopulated - self destruct.
		elif va50 > 11
			setv va00 1
			doif va00 = 2
				base 0
				frat 10
				anim [76 76 57 57 38 38 19 19 0 0]
				over
				kill ownr
				stop
			endi
		endi
*Loop to create seeds
		reps va51
			inst
			new: simp 2 3 8 "gumin" 9 95 4201
			attr 197


			bhvr 32

			elas 20
			perm 99
			accg 0
			aero 0
			fric 100
			clac -1
			puhl -1 15 60

			setv ov86 0

			setv ov87 0

			setv ov01 230
			setv ov61 15
			setv va45 rand va40 va41
			setv va46 rand va42 va43
			seta va16 targ
			doif tmvt va45 va46 = 1
				mvto va45 va46
				setv ov90 0
				tick rand 2 5
				slow
			else
				targ va16
				kill targ
				targ ownr
				base 0
				frat 10
				anim [76 76 57 57 38 38 19 19 0 0]
				over
				kill ownr
				stop
			endi
		repe
		targ ownr
*Allow for parent plant to reproduce again.
		addv ov92 1
		wait rand 100 300
		doif ov92 <= 2
			setv ov91 0
		endi
	retn
endm

*Seed timer script
scrp 2 3 8 9

*Lose lifespan each time script is run
	subv ov01 1


*If too old, die.
	doif ov01 <= 0
		gsub deth
	endi

*Ensure seed is in correct environment
	gsub room

*Step through growth phases
	doif ov90 = 0
		pose 1
		wait rand 40 60
		addv ov90 1
	elif ov90 = 1
		pose 2
		wait rand 40 60
		addv ov90 1
	elif ov90 = 2
		pose 3
		wait rand 40 60
		addv ov90 1
	elif ov90 = 3
		pose 4
		wait rand 40 60
		addv ov90 1
	elif ov90 = 4
		pose 5
		addv ov90 1
		attr 199
		wait rand 50 400
		setv ov90 5
	elif ov90 = 5
		gsub anim
		gsub rndm
		gsub obst
		gsub move
*Growth opportunity check if old enough
		doif ov01 <= 300
*Environmental checks and chance to grow
			doif obst down < 30 and obst _up_ > 225 and obst rght > 20 and obst left > 20
				etch 2 4 8
					addv va40 1
				next
				etch 2 4 10
					addv va40 1
				next
				etch 2 4 7
					addv va40 1
				next
				doif va40 = 0 and rtyp room ownr = 9 and carr = null
					gsub grow
				endi
			endi
		endi
	endi

*Check for death flag
	doif ov99 = 99
		gsub deth
	endi

*Creates a new adult where the seed was previously (to prevent bounding box errors)
	subr grow
		setv va40 posl
		setv va41 post
		setv ov98 0
		inst
		new: simp 2 4 10 "gumin" 95 0 4200
		attr 197
		perm 100
		elas 20
		clac -1
		accg 1
		aero 5
		fric 100
		mvto va40 va41
		setv ov01 230
		setv ov61 50
		setv ov90 0
		tick 3
		doif carr = null
			kill ownr
		elif carr <> null
			gsub deth
		endi
	retn

*Kills agent directly or sets death flag for next time if carried.
	subr deth
		doif carr = null
			kill ownr
		elif carr <> null
			setv ov99 99
		endi
	retn

*Random movement subroutine
	subr rndm
		loop
			setv ov10 rand -2 2
		untl ov10 <> 0
		doif obst down < 500
			loop
				setv ov11 rand -1 1
			untl ov11 <> 0
		elif obst down >= 500
			loop
				setv ov11 rand -1 2
			untl ov11 <> 0
		endi
	retn

*Animation for seed.
	subr anim
		base 5
		anim [0 1 2 3 255]
	retn

*Obstacle avoidance logic
	subr obst
		doif obst left < 50
			setv ov10 rand 1 2
		endi
		doif obst rght < 50
			setv ov10 rand -2 -1
		endi
		doif obst _up_ < 20
			setv ov11 1
		endi



	retn

*Room monitor - adds gravity if not in the right room and eventually kills seed if not in right room.
	subr room
		doif rtyp room ownr <> 9
			loop
				addv ov86 1
				doif ov86 >= 0 and ov86 <= 6
					addv ov87 .03
				elif ov86 > 6 and ov86 < 200
					addv ov87 .08
				endi
				accg ov87
			untl rtyp room ownr = 9 or ov86 >= 200
			doif ov86 >= 200
				wait rand 100 400
				gsub deth
			endi
		endi
		doif rtyp room ownr = 9
			setv ov86 0
			setv ov87 0
		endi
		accg ov87
	retn


*Applies velocity to ov10 and ov11.
	subr move
		velo ov10 ov11
	retn
endm

*Seed pickup script
scrp 2 3 8 4

endm

*seed drop script
scrp 2 3 8 5
*Drop forces seed into fully active state immediately.
	setv ov90 5
endm

***REMOVAL
rscr
enum 2 4 10
	kill targ
next
enum 2 3 8
	kill targ
next
scrx 2 3 8 9
scrx 2 4 10 9
scrx 2 3 8 4
scrx 2 3 8 5
endm
