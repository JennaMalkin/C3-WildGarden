*GB Template
*# Pray-File "GB_Nudibranch.agent"
*# DS-Name "Nudibranch"
*# Depend nudibranch.c16
*# Attach gb_c3_conv2.c16

*# desc = "C3 Nudibranch raw, buggy, unsupported conversion."
*# Agent Animation File = "gb_c3_conv2.c16"
*# Agent Animation Gallery = "gb_c3_conv2"
*# Agent Sprite First Image = 10
*# GB_Category = 5
*# Agent Author = "Creature Labs"


new: simp 2 15 21 "nudibranch" 0 0 2000
attr 192
elas 50
accg 0
aero 7
perm 75
fric 99
clac -1
setv va77 rand 0 1
setv ov01 rand 2000 2100
setv ov02 rand 2000 2100
setv ov61 60

mvsf game "ch_x" game "ch_y"




pose 46

setv ov30 46
setv ov31 59

tick 10
base ov30
frat 2
anim [0 1 2 3 4 5 6 7 8 9 10 11 12 11 10 9 8 7 6 5 4 3 2 1 0 255]


scrp 2 15 21 9
	gsub room


	subv ov02 1


	subv ov01 1


	doif ov01 <= 0 or ov99 = 99 or ov02 <= 0
		gsub death
	endi


	doif ov02 <= 500
		gsub feed
	endi


	doif ov01 <= 1500 and OV05 = 0
		inst
		rnge 200
		doif obst left > 150 and obst rght > 150 and obst _up_ > 150 and obst down > 150 and CARR = null and rtyp room targ = 9
			velo 0 0
			setv ov30 23
			setv ov31 36
			base ov30
			frat 2
			anim [0 1 2 3 4 5 6 7 8 9 10 11 12 11 10 9 8 7 6 5 4 3 2 1 0 255]
			setv ov05 1
		endi
		slow
	elif ov01 <= 1000 and ov05 = 1
		inst
		rnge 200
		doif obst left > 150 and obst rght > 150 and obst _up_ > 150 and obst down > 150 and CARR = null and rtyp room targ = 9

			velo 0 0
			setv ov30 0
			setv ov31 13
			base ov30
			setv ov05 2
			frat 2
			anim [0 1 2 3 4 5 6 7 8 9 10 11 12 11 10 9 8 7 6 5 4 3 2 1 0 255]
		endi
		slow
	endi


	doif ov05 = 2
		gsub breed
	endi


	subr breed
		doif CARR = null and rtyp room targ = 9
			rnge 1000
			inst
			esee 2 15 21
				doif targ <> null
					addv va00 1
				endi
			next
			slow
			doif va00 <= 3
				snde "nudi"
				reps 2
					inst
					targ ownr
					setv va01 posl
					setv va02 post
					new: simp 2 15 21 "nudibranch" 0 0 2000
					attr 192
					elas 50
					accg 0
					aero 7
					perm 75
					fric 99
					clac -1
					setv va77 rand 0 1
					setv ov01 rand 2000 2100
					setv ov02 rand 2000 2100
					setv ov61 60
					pose 46
					doif tmvt va01 va02 = 1
						mvto va01 va02
					else
						mvsf va01 va02
					endi


					setv ov30 46
					setv ov31 59
					base ov30
					frat 2
					anim [0 1 2 3 4 5 6 7 8 9 10 11 12 11 10 9 8 7 6 5 4 3 2 1 0 255]

					tick 10
					slow
				repe
				targ ownr

				gsub death
			endi
		endi
	retn



	subr feed
		setv va09 0
		setv va10 0
		inst

		rnge 75
		esee 2 13 8
			doif targ <> null
				addv va09 1
				doif va09 = 1
					kill targ
					setv va10 99
				endi
			endi
		next
		slow
		setv va09 0
		doif va10 = 0
			inst

			rnge 75
			esee 2 15 18
				doif targ <> null
					addv va09 1
					doif va09 = 1
						kill targ
						setv va10 99
					endi
				endi
			next
			slow
		endi
		setv va09 0
		doif va10 = 0
			inst

			rnge 75
			esee 2 3 8
				doif targ <> null
					addv va09 1
					doif va09 = 1
						kill targ
						setv va10 99
					endi
				endi
			next
			slow
		endi
		doif va10 = 99
			gsub grab
			stop
		endi
	retn


	subr grab
		anim []
		base ov31
		anim [0 1 2 3 4 5 6 7 8 9]
		over
		base ov30
		frat 2
		anim [0 1 2 3 4 5 6 7 8 9 10 11 12 11 10 9 8 7 6 5 4 3 2 1 0 255]
		addv ov01 5000
	retn


	subr death
		doif ov05 = 2
			gsub breed
		endi
		doif carr = null
			velo 0 0
			wait 200
			snde "ndid"
			wait 10
			kill ownr
		elif carr <> null
			doif carr = pntr
				setv ov99 99
			else
				velo 0 0
				wait 200
				snde "ndid"
				wait 10
				kill ownr
			endi
		endi
	retn



	subr room
		doif rtyp room ownr <> 9
			loop
				addv ov86 1
				doif ov86 >= 0 and ov86 <= 6
					addv ov87 .03
				elif ov86 > 6 and ov86 < 200
					addv ov87 .08
				endi
				accg ov87
			untl rtyp room ownr = 9 or ov86 >= 200
			doif ov86 >= 200
				wait rand 100 400
				gsub death
			endi
		endi
		doif rtyp room ownr = 9
			setv ov86 0
			setv ov87 0
			gsub drft
		endi
		accg ov87
	retn

	subr drft
		setv va70 rand 1 4
		doif va70 = 1
			setv ov11 -1
		elif va70 = 2
			setv ov11 1
		elif va70 = 3
			setv ov10 -1
		elif va70 = 4
			setv ov10 1
		endi
		gsub obst
		gsub move
	retn

	subr obst
		doif obst 0 < 50
			setv ov10 1
		endi
		doif obst 1 < 50
			setv ov10 -1
		endi
		doif obst 2 < 50
			setv ov11 1
		endi
		doif obst 3 < 20
			setv ov11 -1
		endi
	retn
	subr anim
		wait rand 1 10
		base ov30
		anim [0 1 2 3 4 5 6 7 8 9 10 11 12 11 10 9 8 7 6 5 4 3 2 1 0]
		over
	retn
	subr move
		velo ov10 ov11
	retn
endm
rscr
enum 2 15 21
	kill targ
next
scrx 2 15 21 9
endm
