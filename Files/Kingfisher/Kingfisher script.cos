*GB Template
*# Pray-File "GB_Kingfisher.agent"
*# DS-Name "Kingfisher"
*# Depend king.c16
*# Attach gb_c3_conv2.c16

*# desc = "C3 Kingfisher conversion. After injection, place nest in an appropriate location."
*# Agent Animation File = "gb_c3_conv2.c16"
*# Agent Animation Gallery = "gb_c3_conv2"
*# Agent Sprite First Image = 13
*# GB_Category = 3
*# Agent Author = "Creature Labs"

*Nest
new: simp 2 17 3 "king" 3 50 50
accg 0
perm 49
tick 400
setv ov61 12
mvsf game "ch_x" game "ch_y"
seta va19 targ
setv ov02 200
*This is the stuff to put it in "placement mode"
setv name "placed" 0
attr 3
pose 2
tint 255 128 128 128 128

*nest drop script
scrp 2 17 3 5
	setv va00 228
	tint va00 128 128 128 128
	reps 10
		doif movs = 0
			wait 10
			subv va00 10
			tint va00 128 128 128 128
		else
			tint 255 128 128 128 128
			stop
		endi
	repe
	inst
	doif movs ne 0
		stop
	endi
	setv name "placed" 1
	tint 128 128 128 128 128
	attr 0
	sndc "beam"
	setv va00 posx
	setv va01 posy
	seta va19 targ
**Create a happy little bird~
	new: simp 2 15 10 "king" 50 0 2000
	accg 3
	perm 49
	tick 6
	attr 195
	aero 10
	bhvr 16

	setv ov00 0
	setv ov01 2000
	setv ov05 2
	setv ov06 rand 0 1
	seta ov16 null
	seta ov17 null
	seta ov18 null
	seta ov19 va19
	setv ov10 1
	setv ov11 -1
	setv ov30 2
	setv ov31 10
	setv ov32 18
	setv ov33 22
	setv ov34 26
	setv ov35 31
	setv ov36 36
	setv ov37 38
	setv ov61 70
	setv ov72 600
	setv ov73 600
	setv ov74 1200
	setv ov75 0
	setv ov77 0
	setv ov02 402

	mvto va00 va01
	setv vely 0
*link the two together~
	seta va19 targ
	targ ov19
	seta ov19 va19
endm
***Kingfisher eat
scrp 2 15 10 12
	stim writ from 80 4
	kill ownr

endm
***Kingfisher Timer
scrp 2 15 10 9
	inst
	addv ov01 1
	subv ov02 1
	addv ov65 1
	doif ov02 < 0
		setv ov00 99
	endi

*	doif ov00 < 98 and ov00 <> 2
*		accg 3
*	endi
* set target of branch in ov17	
	doif ov17 = null
		rtar 1 1 5
		seta va88 targ
		targ ownr
		seta ov17 va88
	endi

	doif obst 0 lt 30
		setv ov10 1
		setv ov11 -1
	endi
	doif obst 1 lt 30
		setv ov10 -1
		setv ov11 -1
	endi

	doif obst 2 lt 30
		setv ov11 0
	endi
	doif obst 3 lt 30
		setv ov11 -1
	endi

	doif ov00 eq 99
		gsub die_
	endi

*If dusk or night and awake
	doif time ge 3 and ov00 = 0
		doif ov19 <> null
			setv ov00 4
		endi
	endi
*if dawn or morning and asleep
	doif time le 1 and ov00 = 98
*		doif ov00 eq 5
		doif ov05 eq 2
			setv ov00 0
			accg 3
		endi
		doif ov05 eq 1
			setv ov00 4
		endi
*		endi
	endi
*if hungry and roaming
	doif ov02 lt ov73 and ov00 = 0
		doif ov05 eq 2
			setv ov00 1
		endi
		doif ov05 eq 1
			doif ov00 ne 5
*				setv ov00 5
*			else
				setv ov00 4
			endi
		endi
	endi

*if not carrying anything and out of water
	doif carr = null
		doif rtyp room ownr <> 8
*		doif posy < 1015
			doif ov00 <> 2 and ov00 < 98
				accg 3
			endi
			doif vely lt 0 and ov02 < ov74
				setv ov00 1
			endi

		else
			accg -1
		endi
	endi
*endi

	doif ov00 eq 5
		doif ov75 eq 1
			kill ov18
			seta ov18 null
			addv ov02 ov72
			doif ov02 gt ov74
				setv ov00 0
			else
				setv ov00 1
			endi
			setv ov75 0
		endi
	endi
	doif ov00 eq 99
		gsub die_
	endi
*if full up
	doif ov02 gt ov73
		doif ov05 eq 1
			setv ov00 0
		endi
	endi

	doif ov00 eq 4
		doif seee ownr ov17 = 1
			gsub gbed
		else
			gsub roam
		endi
	endi


	doif ov00 eq 3
		gsub fish
	endi

	doif ov00 eq 2
		gsub gfsh
	endi

	doif ov00 eq 1
		doif seee ownr ov17 = 1
			gsub gbrh
		else
			gsub roam
		endi
	endi


	doif ov00 eq 0
		gsub roam
	endi


*	SUBROUTINES


	subr die_
		setv va50 posx
		setv va51 posy
		setv va52 ov10
		subv va50 20
		subv va51 8
		new: simp 2 10 8 "king" 8 16 2000
		accg 2
		perm 99
		elas 0
		fric 50
		tick 4
		attr 195
		aero 0
		setv ov10 va52
		setv ov01 0
		setv ov61 30
		doif tmvt va50 va51 <> 1
			kill targ
			kill ownr
			stop
		endi
		mvto va50 va51
		setv vely 0
		targ ownr
		kill targ
	retn

	subr gfsh
		setv va47 2
		setv va48 15
		setv va49 0
		gsub find

		targ ov16

		doif targ ne null
			targ ownr
* use special near (ner2) for kingfisher
			gsub ner2
			doif va40 eq 1
				setv velx 1
				targ ov16
				setv va50 velx
				targ ownr
				accg 3
				setv ov00 3
				mulv va50 2
				setv velx va50
				doif velx le 0
					setv ov10 -1
					pose 0
				else
					setv ov10 1
					pose 1
				endi
			endi
		else
			targ ownr
			doif ov02 <= ov74
				setv ov00 2
			else
				setv ov00 0
			endi
		endi
		targ ownr
		stop
	retn

	subr fish
		targ ov16
		doif targ <> null
			targ ownr
			gsub ani2
			doif touc ownr ov16 <> 0
				mesg writ ov16 4
				setv ov75 1
				setv ov00 1
				seta ov18 ov16
			endi
			doif vely lt 0
				doif carr = null
*					doif rtyp room ownr <> 8
					setv ov00 1
*					endi
				endi
			endi
		else
			targ ownr
			setv ov00 0
		endi
		targ ownr
		stop
	retn

	subr gbrh
		seta ov16 ov17
		targ ov16
		doif targ <> null
			targ ownr
			gsub hunt

			doif touc ownr ov16 <> 0
				targ ov16
				setv va50 posx
				setv va51 posy
				subv va50 32
				subv va51 22
				targ ownr
				setv ov00 2
				setv velx 0
				setv vely 0
				accg 0
				mvto va50 va51
				base 0
				doif ov10 le 0
					pose 0
				else
					pose 1
				endi
				doif ov75 eq 1
					gsub gobl
				endi
				stop
			endi
		else
			targ ownr
			seta ov17 null
			setv ov00 0
		endi
		targ ownr
		gsub vect
		subv va10 6
		subv va11 6
		gsub anim
		gsub move
		stop
	retn


	subr gobl
		targ ov18
		doif targ <> null
			targ ownr
			doif ov10 le 0
				base ov36
				anim [0 1 0 1 0 1]
				over
				inst
				doif ov18 ne null
					kill ov18
					seta ov18 null
				endi
				slow
				setv ov75 0
			endi
			doif ov10 gt 0
				base ov37
				anim [0 1 0 1 0 1]
				over
				inst
				doif ov18 ne null
					kill ov18
					seta ov18 null
				endi
				slow
				setv ov75 0
			endi
			addv ov02 ov72
			doif ov02 gt ov74
				setv ov00 0
				accg 3
			else
				setv ov00 1
			endi
		else
			targ ownr
			setv ov00 1
			setv ov75 0
		endi
	retn

	subr gbed
		seta ov16 ov19
		targ ov16
		doif targ <> null
			targ ownr
			gsub hunt
			doif touc ownr ov16 <> 0
				targ ownr

				setv velx 0
				setv vely 0
				accg 0
				base 0
				doif ov05 eq 2
					doif ov10 <= 0
						pose 40
					else
						pose 40
					endi
				endi
				doif ov05 eq 1
					doif ov10 <= 0
						pose 40
					else
						pose 40
					endi

					addv ov02 2
					doif ov02 gt ov74
						setv ov00 0
					endi
				endi
				gsub slep
				stop
			endi
		else
			targ ownr
			seta ov19 null
			setv ov00 0
		endi
		targ ownr
		gsub vect
		gsub anim
		gsub move
		stop

	retn

	subr slep
		setv ov00 98
		tick 20
		stop
	retn


	subr roam
		setv va66 rand 0 10

		doif va66 lt 5
			doif ov10 ne 0
				setv ov10 0
			else
				setv ov10 rand -1 1
			endi
		endi
		doif va66 gt 5
			setv ov11 rand -1 1
		endi
		doif obst down lt 150
			setv ov11 -1
			setv vely -10
		endi


		setv va60 prop grid ownr left 5
		setv va61 prop grid ownr rght 5
		setv va62 prop grid ownr _up_ 5
		setv va63 prop grid ownr down 5
		doif va60 > va61
			setv ov10 -1
		elif va60 < va61
			setv ov10 1
		endi

		doif obst left lt 150
			setv ov10 1
			setv velx 10
		endi
		doif obst rght lt 150
			setv ov10 -1
			setv velx -10
		endi
		doif obst _up_ lt 150
			setv ov11 1
			setv vely 10
		endi
*		doif va62 > va63
*			setv ov11 1
*		elif va62 < va63
*			setv ov11 -1
*		endi


		gsub vect
		gsub anim
		gsub move
		stop
	retn

	subr vect
		setv va10 rand 6 20
		setv va11 rand 15 20
	retn



	subr anim
		doif ov10 eq 0
			loop
				setv ov10 rand -1 1
			untl ov10 ne 0
		endi

		doif ov10 lt 0
			pupt -1 8 24
			base ov30
			anim [0 1 2 3 4 5 6 7 255]
		endi

		doif ov10 gt 0
			pupt -1 58 24
			base ov31
			anim [0 1 2 3 4 5 6 7 255]
		endi
	retn

	subr ani2
		doif vely ge 0
			doif ov10 lt 0
				base ov32
				anim [0 1 2 3]
			endi

			doif ov10 gt 0
				base ov33
				anim [0 1 2 3]
			endi
		endi
		doif vely lt 0
			doif ov10 lt 0
				base ov34
				anim [0 1 2 3 4]
			endi

			doif ov10 gt 0
				base ov35
				anim [0 1 2 3 4]
			endi
		endi

	retn

	subr move
		mulv va10 ov10
		mulv va11 ov11
		setv velx va10
		setv vely va11
	retn

* uses va47,48,49 as passed in family, genus, species variables
* returns ov16 as target 
	subr find
		setv va99 99999999
		seta va58 null
*		enum va47 va48 va49
		inst
*		doif va48 eq 13
*			doif va49 eq 5
*				setv va88 1
*			endi
*		endi

		esee va47 va48 va49
			doif targ <> null
				setv va88 0
				setv va50 relx ownr targ
				setv va51 rely ownr targ
				doif va50 lt 0
					negv va50
				endi
				doif va51 lt 0
					negv va51
				endi
				setv va52 va50
				mulv va52 va52
				setv va53 va51
				mulv va53 va53
				setv va54 va52
				addv va54 va53
				doif va54 lt va99
					seta va58 targ
					setv va99 va54
				endi
			endi
		next
		targ va58
		doif targ <> null
			targ ownr
			seta ov16 va58
		else
			targ ownr
			seta ov16 null
		endi
		targ ownr
*		slow
	retn

* change ov10 and ov11 multipliers to head towards current target (ov16)
	subr hunt
		inst
		targ ov16
		doif targ <> null
			setv va50 posx
			setv va51 posy
			targ ownr
			doif posx lt va50
				setv ov10 1
			endi
			doif posx gt va50
				setv ov10 -1
			endi
			doif posy lt va51
				setv ov11 1
			endi
			doif posy gt va51
				setv ov11 -1
			endi
		else
			targ ownr
			seta ov16 null
			setv ov00 0
		endi
	retn

	subr hun2
		targ ov16
		setv va50 posx
		setv va51 posy
		targ ownr
		doif posx lt va50
			setv ov10 1
		endi
		doif posx gt va50
			setv ov10 -1
		endi
		doif posy lt va51
			setv ov11 1
		endi
		doif posy gt va51
			setv ov11 -1
		endi
	retn



	subr ner2
		setv va66 0
* changed to allow for all critters?
		esee 2 15 0
			addv va66 1
		next
		doif va66 > 4
			setv va40 0
			doif ov16 <> null
				inst
				targ ov16
				setv va50 posx
				setv va51 posy
				setv va52 posx
				setv va53 posy
				subv va50 20
				subv va51 800
				addv va52 20
				addv va53 0
				targ ownr
				doif posx gt va50
					doif posy gt va51
						doif posx lt va52
							doif posy lt va53
								setv va40 1
							endi
						endi
					endi
				endi

			endi
		endi
	retn




endm
***Kingfisher Dropped
scrp 2 15 10 5
	setv ov00 0
	accg 3
endm
***Rotting Kingfisher
scrp 2 10 8 9
	addv ov01 1
	doif ov01 gt 10
		doif carr = null
			doif ov10 le 0
				anim [0 1 2 3]
				doif room targ <> -1 and carr = null
					altr room targ 3 0.1
					altr room targ 4 0.2
				endi
				over
			endi
			doif ov10 gt 0
				anim [4 5 6 7]
				doif room targ <> -1 and carr = null
					altr room targ 3 0.1
					altr room targ 4 0.2
				endi
				over
			endi
			kill targ
			stop
		endi
	endi
endm

scrp 2 17 3 9
	doif name "placed" eq 0
		stop
	endi
	doif ov19 eq null
		setv va50 posx
		setv va51 posy
		inst
		new: simp 2 15 10 "king" 50 0 2000
		accg 3
		perm 49
		tick 6
		attr 195
		aero 10
* ov00 = state (0=roam,1=to branch,2=look 4 fish,3=fish,4=gbed,5=slep,99=dead)
* ov01 = age
* ov06 = gender
* ov16 = target of hunt
* ov17 = target of branch
* ov18 = carried fish target
* ov72 = energy gained from eating food
* ov73 = energy threshold before hunting begins
* ov74 = energy threshold before before hunting stopped
* ov75 = got fish flag
* ov77 = experimental 'wetness' variable
* ov02 = energy
		setv ov00 0
		setv ov01 2000
		setv ov05 2
		setv ov06 rand 0 1
		seta ov16 null
		seta ov17 null
		seta ov18 null
		seta ov19 ownr
		setv ov10 1
		setv ov11 -1
		setv ov30 2
		setv ov31 10
		setv ov32 18
		setv ov33 22
		setv ov34 26
		setv ov35 31
		setv ov36 36
		setv ov37 38
		setv ov61 70
		setv ov72 800
		setv ov73 600
		setv ov74 1200
		setv ov75 0
		setv ov77 0
		setv ov02 402

		mvto va50 va51
		setv vely 0

		seta va19 targ
		targ ov19
		seta ov19 va19
	endi
endm


rscr

enum 2 15 10
	kill targ
next
scrx 2 15 10 9

enum 2 17 3
	kill targ
next
scrx 2 17 3 9

enum 2 10 8
	kill targ
next
scrx 2 10 8 9

enum 1 1 5
	kill targ
next
scrx 1 1 5 9


endm
